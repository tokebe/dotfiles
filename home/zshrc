# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Load modules
zmodload zsh/complist
autoload -U compinit && compinit
autoload -U colors && colors

# Load Antidote (plugin manager)
source "${ZDOTDIR:-$HOME}/.antidote/antidote.zsh"

# Completion (cmp) Options
zstyle ':completion:*' menu select # Tab opens cmp menu
zstyle ':completion:*' squeeze-slashes false # Allow /*/ expansion
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' # Smart case sensitivity
zstyle ':completion:*:git-checkout:*' sort false # Don't sort `git checkout`

setopt auto_menu menu_complete # Use first match and show menu
setopt auto_param_slash # Add slash to directory completions

# General Options
unsetopt prompt_sp # Don't autoclean blanklines

## Globbing
# setopt no_case_glob no_case_match # Case insensitive
setopt extended_glob # Match # ~ ^
setopt glob_dots # Include dotfiles without explicit `.`
setopt interactive_comments # Allow comments in interactive shell

## History
setopt append_history # Allow parallel session history
setopt inc_append_history # Append history incrementally
setopt share_history # Keep history between sessions
setopt extended_history # Keep timestamps
# setopt share_history # Share history between sessions
HISTSIZE=1000000 # larger history size
SAVEHIST=1000000
HISTFILE="${XDG_CACHE_HOME:-$HOME/.cache}/.zsh_history" # XDG-compliant history
HISTCONTROL=ignoreboth # Ignore consecutive duplicates and space-starting commands

# Plugins
antidote load

## Plugin Config

### fzf-tab
zstyle ':completion:*' menu no # Hand control to fzf-tab
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS} # enable filename colorizing
# zstyle ':fzf-tab:*' use-fzf-default-opts yes
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ctpv $realpath' # use ctpv for previewing
zstyle ':fzf-tab:*' fzf-flags \
  --height="~50%" --prompt="󰥨 " \
  --info="inline-right" --no-separator \
  --preview-window="border-sharp" \
  --scrollbar="▐"
# switch group using `<` and `>`
zstyle ':fzf-tab:*' switch-group '<' '>'

## zsh-users/zsh-history-substring-search
export HISTORY_SUBSTRING_SEARCH_FUZZY=true

bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

## magic-enter
MAGIC_ENTER_GIT_COMMAND='$HOME/.zsh.dashboard git'
MAGIC_ENTER_OTHER_COMMAND='$HOME/.zsh.dashboard'

## ohmyzsh/alias-finder
# export ZSH_ALIAS_FINDER_PREFIX='󱉵 Has Alias: '

# Bindings

## CTRL-left/right to skip words
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

## Alt-left/right to skip words
bindkey "^[[1;3C" forward-word
bindkey "^[[1;3D" backward-word

## Home/End
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line

bindkey "^H" kill-word # Delete quick

if ! typeset -f magic-tab-cmd >/dev/null; then
    function magic-tab-cmd {
      print -rlo -- $commands:t | fzf --height="~50%" --prompt="▎ " \
      --info="inline-right" --no-separator --scrollbar="▐" --layout="reverse" \
      --gutter="▎" \
      --color="gutter:8"
    }
fi
if ! typeset -f magic-space-tab-cmd >/dev/null; then
	function magic-space-tab-cmd {
	  fd -ut f  | fzf --height="~50%" --prompt="▎ " \
	  --info="inline-right" --no-separator --scrollbar="▐" --layout="reverse" \
	  --gutter="▎" \
	  --color="gutter:8" --preview="ctpv {}"
	}
fi

function magic-tab {
  # Only run magic-double-tab commands when the command line is empty and
  # when on the first line (PS1)
  if ! (( $#BUFFER )) && [[ "$CONTEXT" == start ]]; then
    LBUFFER+=$(magic-tab-cmd)
    zle redisplay
  elif [[ $BUFFER = " " ]] && [[ "$CONTEXT" == start ]]; then
    file=$(magic-space-tab-cmd) && nvim $file
    BUFFER=""
    zle redisplay
  else
    zle fzf-tab-complete
  fi
}
zle -N magic-tab
bindkey '\t' magic-tab


# Functions and Aliases

# mkdir + cd
function md() { [[ $# == 1 ]] && mkdir -p -- "$1" && cd -- "$1" }
compdef _directories md

# For bat
alias bathelp='bat --plain --language=help'
help() {
    "$@" --help 2>&1 | bathelp
}


# TODO Ctrl-Shift+/ -> show help for command

alias tree='tree -a -I .git'
alias rangerd='ranger --choosedir=$HOME/.rangerdir; LASTDIR=`cat $HOME/.rangerdir`; cd "$LASTDIR"'
alias gitui=lazygit
alias lg=lazygit
alias ls='lsd -AX --group-dirs=first'
alias lso="lsd -AXtrl --color=always | sed -re 's/^([^ ]* ){3}//'" # Find old files
alias notes="( cd ~ && nvim ~/notes )"
alias cat=ccat
alias lk='() { walk "$@" --icons; }'
alias lkd='() { cd "$(walk "$@" --icons --preview)" }'
alias pn=pnpm
alias tm='gtrash put'
alias tr='gtrash restore'
alias f='spf'
if command -v neovide >/dev/null 2>&1; then
    alias nvd='neovide --frame buttonless --multigrid'
fi
alias st=systemctl-tui

# Integration
## FZF Integration
source <(fzf --zsh)
fzf_inline_opts='--height="~50%" --prompt="▎󰥨 "
--info="inline-right" --no-separator
--preview-window="border-sharp"
--gutter="▎"
--color="gutter:8"
--scrollbar="▐"
--preview="ctpv {}"'
export FZF_ALT_C_OPTS="$fzf_inline_opts"
export FZF_CTRL_T_OPTS="$fzf_inline_opts"
export FZF_CTRL_R_OPTS='--height="~50%" --prompt="▎ "
--info="inline-right" --no-separator
--gutter="▎" --color="gutter:8"
--scrollbar="▐" --layout="reverse"'

# Ensure tmux uses utf8
export LC_ALL=en_IN.UTF-8
export LANG=en_IN.UTF-8

# Use bat for manpager
export MANPAGER=$HOME/.batman

# Get nvm working
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

# Make Go work
export GOPATH=$HOME/gocode
export PATH="$HOME/gocode/bin:$PATH"

# pnpm
export PNPM_HOME="$HOME/.pnpm"
case ":$PATH:" in
  *":$PNPM_HOME:"*) ;;
  *) export PATH="$PNPM_HOME:$PATH" ;;
esac
# pnpm end

# Navi integration
eval "$(navi widget zsh)"

# fx configuration https://github.com/antonmedv/fx
export FX_SHOW_SIZE=true
export FX_LINE_NUMBERS=true

# walk config
export WALK_REMOVE_CMD='gtrash put'

# jump
eval "$(jump shell)"

# Fix Cargo pkgconfig on Fedora for some libraries
export PKG_CONFIG_PATH=/usr/lib64/pkgconfig


### MANAGED BY RANCHER DESKTOP START (DO NOT EDIT)
export PATH="/Users/jcallaghan/.rd/bin:$PATH"
### MANAGED BY RANCHER DESKTOP END (DO NOT EDIT)

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Prompt at bottom
bottom_prompt() {
  print ${(pl:$LINES::\n:):-}
}
alias clear='clear && bottom_prompt'

bottom_prompt
